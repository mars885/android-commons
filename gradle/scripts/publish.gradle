import java.util.zip.ZipFile

apply plugin: "maven-publish"
apply plugin: "com.github.dcendents.android-maven"
apply plugin: "com.jfrog.bintray"


group = publishingConfig.releaseGroupId
version = publishingConfig.releaseVersion

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId = publishingConfig.releaseGroupId
            artifactId = publishingConfig.releaseArtifact
            version = publishingConfig.releaseVersion

            // Tell maven to prepare the generated "*.aar" file for publishing
            artifact(androidSourcesJar)
            artifact("$buildDir/outputs/aar/${project.archivesBaseName}-release.aar")
        }
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            packaging "aar"
            groupId publishingConfig.releaseGroupId
            artifactId publishingConfig.releaseArtifact

            name publishingConfig.releaseArtifact
            description publishingConfig.releaseDescription
            url publishingConfig.releaseWebsite

            licenses {
                license {
                    name publishingConfig.licenseName
                    url publishingConfig.licenseUrl
                }
            }

            developers {
                developer {
                    id publishingConfig.developerId
                    name publishingConfig.developerName
                    email publishingConfig.developerEmail
                }
            }

            scm {
                connection publishingConfig.gitUrl
                developerConnection publishingConfig.gitUrl
                url publishingConfig.siteUrl
            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = "sources"
    from android.sourceSets.main.java.srcDirs
}

// a temporary replacement
task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.srcDirs
    // Also add the generated R class to avoid errors...
    // TODO: debug is hard-coded
    source += "$buildDir/generated/source/r/debug/"
    // ... but exclude the R classes from the docs
    excludes += "**/R.java"

    // TODO: "compile" is deprecated in Gradle 4.1,
    // but "implementation" and "api" are not resolvable :(
    classpath += configurations.compile

    afterEvaluate {
        // Wait after evaluation to add the android classpath
        // to avoid "buildToolsVersion is not specified" error
        classpath += files(android.getBootClasspath())

        // Process AAR dependencies
        def aarDependencies = classpath.filter { it.name.endsWith('.aar') }
        classpath -= aarDependencies
        aarDependencies.each { aar ->
            System.out.println("Adding classpath for aar: " + aar.name)
            // Extract classes.jar from the AAR dependency, and add it to the javadoc classpath
            def outputPath = "$buildDir/tmp/exploded-aar/${aar.name.replace('.aar', '.jar')}"
            classpath += files(outputPath)

            // Use a task so the actual extraction only happens before the javadoc task is run
            dependsOn task(name: "extract ${aar.name}").doLast {
                extractZipEntry(aar, 'classes.jar', outputPath)
            }
        }
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = propertyOrDefault("bintrayUser")
    key = propertyOrDefault("bintrayApiKey")

    configurations = ["archives"]

    pkg {
        repo = publishingConfig.releaseRepoName
        //userOrg = releaseUserOrg
        name = publishingConfig.releaseArtifact
        desc = publishingConfig.releaseDescription
        websiteUrl = publishingConfig.siteUrl
        vcsUrl = publishingConfig.gitUrl
        issueTrackerUrl = publishingConfig.issueTracker
        githubReleaseNotesFile = publishingConfig.releaseNotesFile
        licenses = publishingConfig.allLicenses
        dryRun = false
        publish = true
        override = false
        publicDownloadNumbers = false

        version {
            desc = publishingConfig.releaseDescription
        }
    }
}

bintrayUpload.dependsOn("install")

// Utility method to extract a single entry from a Zip Archive
private def extractZipEntry(File archiveFile, String entryPath, String outputPath) {
    if(!archiveFile.exists()) {
        throw new GradleException("Archive File $archiveFile not found")
    }

    def zipFile = new ZipFile(archiveFile)

    zipFile.entries().each {
        if(it.name == entryPath) {
            def path = Paths.get(outputPath)

            if(!Files.exists(path)) {
                Files.createDirectories(path.getParent())
                Files.copy(zip.getInputStream(it), path)
            }
        }
    }
    zip.close()
}

String propertyOrDefault(String propertyName, String defaultValue = "") {
    def propertyValue = project.properties[propertyName]
    return ((propertyValue != null) ? propertyValue : defaultValue)
}